import org.jose4j.jwk.JsonWebKey
import org.jose4j.jwk.OctJwkGenerator

apply from: 'https://raw.github.com/akhikhl/gretty/master/pluginScripts/gretty.plugin'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.bitbucket.b_c:jose4j:0.4.4"
    }
}

gretty {
    httpEnabled = false
    httpsEnabled = true
    jvmArgs = [
            "-Dservice.registry.config.location=file://${project.file('etc/cas/services')}"
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = project."gradle.version"
}

task generateKey << {
    int keySize = project.hasProperty('size') ? size.toInteger() : 512
    println "generating key of size ${keySize} (to change, pass `-Psize=int` where `int` is the size)"
    def octetKey = OctJwkGenerator.generateJwk(keySize)
    def params = octetKey.toParams(JsonWebKey.OutputControlLevel.INCLUDE_SYMMETRIC)
    println "key: ${params.get('k')}"
}